import groovy.json.JsonSlurper

plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

android {
    namespace "com.ironcorelabs.sdk"
    compileSdkVersion 36
    buildToolsVersion "36.0.0"
    ndkVersion "27.3.13750724"

    defaultConfig {
        minSdkVersion 31
        targetSdkVersion 36
        versionCode 1
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'Assert'
        abortOnError false
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.ironcorelabs'
            artifactId = 'ironoxide-android'

            pom {
                name = 'ironoxide-android'
                packaging = 'aar'
                description = 'Android library for IronOxide transform encryption'
                url = 'https://github.com/IronCoreLabs/ironoxide-java'
                scm {
                    url = 'https://github.com/IronCoreLabs/ironoxide-java'
                    connection = 'scm:git@github.com:IronCoreLabs/ironoxide-java.git'
                    developerConnection = 'scm:git@github.com:IronCoreLabs/ironoxide-java.git'
                }
                licenses {
                    license {
                        name = 'AGPL-3.0'
                        url = 'https://www.gnu.org/licenses/agpl-3.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer { id = 'bobwall23'; name = 'Bob Wall'; url = 'https://github.com/bobwall23' }
                    developer { id = 'coltfred'; name = 'Colt Frederickson'; url = 'https://github.com/coltfred' }
                    developer { id = 'giarc3'; name = 'Craig Colegrove'; url = 'https://github.com/giarc3' }
                    developer { id = 'skeet70'; name = 'Murph Murphy'; url = 'https://github.com/skeet70' }
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.release
}

// Locate the rustls-platform-verifier-android classes from the cargo dependency tree and
// bundle them directly, so consumers don't need to resolve the unpublished rustls Maven repo.
// Based on https://github.com/signalapp/libsignal/blob/a4a0663/java/android/build.gradle#L84-L113
// See https://github.com/rustls/rustls-platform-verifier/issues/115
File findRustlsPlatformVerifierClasses() {
    def dependencyText = providers.exec {
        it.workingDir = new File("../")
        commandLine("cargo", "metadata", "--format-version", "1")
    }.standardOutput.asText.get()

    def dependencyJson = new JsonSlurper().parseText(dependencyText)
    def manifestPath = file(dependencyJson.packages.find { it.name == "rustls-platform-verifier-android" }.manifest_path)

    // Modifications here:
    // Rather than use the Maven repository in the crates.io source, reference the AAR directly by path.
    // Then, extract the classes from it so that we have a self-contained library.
    def aar = fileTree(manifestPath.parentFile).matching {
        include "maven/rustls/rustls-platform-verifier/*/rustls-platform-verifier-*.aar"
    }.getSingleFile()
    def classesJar = zipTree(aar).matching {
        // Make sure there's nothing in the AAR that we haven't accounted for.
        // This isn't perfect: there could be something important in AndroidManifest.xml.
        // But it's likely good enough.
        exclude "AndroidManifest.xml"
        exclude "META-INF/com/android/build/gradle/aar-metadata.properties"
        exclude "R.txt"
    }.getSingleFile()
    assert classesJar.name == "classes.jar"
    return classesJar
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation files(findRustlsPlatformVerifierClasses())
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications {
            release {
                from components.release
            }
        }
    }
}
