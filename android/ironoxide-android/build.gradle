plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.3"
    ndkVersion "26.3.11579264"

    defaultConfig {
        minSdkVersion 29
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'x86', 'arm64-v8a', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'Assert'
        abortOnError false
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.sourceFiles
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.ironcorelabs'
            artifactId = 'ironoxide-android'
            version = android.defaultConfig.versionName

            afterEvaluate {
                from components.release
            }

            artifact androidSourcesJar

            pom {
                name = 'ironoxide-android'
                packaging = 'aar'
                description = 'Android library for IronOxide transform encryption'
                url = 'https://github.com/IronCoreLabs/ironoxide-java'

                scm {
                    url = 'https://github.com/IronCoreLabs/ironoxide-java'
                    connection = 'scm:git@github.com:IronCoreLabs/ironoxide-java.git'
                    developerConnection = 'scm:git@github.com:IronCoreLabs/ironoxide-java.git'
                }

                licenses {
                    license {
                        name = 'AGPL-3.0'
                        url = 'https://www.gnu.org/licenses/agpl-3.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'bobwall23'
                        name = 'Bob Wall'
                        url = 'https://github.com/bobwall23'
                    }
                    developer {
                        id = 'coltfred'
                        name = 'Colt Frederickson'
                        url = 'https://github.com/coltfred'
                    }
                    developer {
                        id = 'giarc3'
                        name = 'Craig Colegrove'
                        url = 'https://github.com/giarc3'
                    }
                    developer {
                        id = 'skeet70'
                        name = 'Murph Murphy'
                        url = 'https://github.com/skeet70'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "sonatype"
            url = version.endsWith("SNAPSHOT")
                    ? uri("https://oss.sonatype.org/content/repositories/snapshots/")
                    : uri("https://oss.sonatype.org/service/local/staging/deploy/maven2")
            credentials {
                username = project.findProperty("NEXUS_USERNAME") ?: ""
                password = project.findProperty("NEXUS_PASSWORD") ?: ""
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(findProperty("signing.keyId"),
                       findProperty("signing.secretKeyRingFile"),
                       findProperty("signing.password"))
    sign publishing.publications.release
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
