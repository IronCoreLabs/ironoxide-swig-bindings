import groovy.json.JsonSlurper

plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

// Locate the rustls-platform-verifier-android Maven repository from the cargo dependency tree.
// This AAR provides the Java-side certificate verification class needed by rustls on Android.
String findRustlsPlatformVerifierProject() {
    def dependencyText = providers.exec {
        it.workingDir = new File(project.rootDir, "../")
        commandLine("cargo", "metadata", "--format-version", "1")
    }.standardOutput.asText.get()

    def dependencyJson = new JsonSlurper().parseText(dependencyText)
    def manifestPath = file(dependencyJson.packages.find {
        it.name == "rustls-platform-verifier-android"
    }.manifest_path)
    return new File(manifestPath.parentFile, "maven").path
}

configurations {
    rustlsEmbed
}

repositories {
    maven {
        url = findRustlsPlatformVerifierProject()
        metadataSources.artifact()
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

android {
    namespace "com.ironcorelabs.sdk"
    compileSdkVersion 36
    buildToolsVersion "36.0.0"
    ndkVersion "27.3.13750724"

    defaultConfig {
        minSdkVersion 31
        targetSdkVersion 36
        versionCode 1
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'Assert'
        abortOnError false
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.ironcorelabs'
            artifactId = 'ironoxide-android'

            pom {
                name = 'ironoxide-android'
                packaging = 'aar'
                description = 'Android library for IronOxide transform encryption'
                url = 'https://github.com/IronCoreLabs/ironoxide-java'
                scm {
                    url = 'https://github.com/IronCoreLabs/ironoxide-java'
                    connection = 'scm:git@github.com:IronCoreLabs/ironoxide-java.git'
                    developerConnection = 'scm:git@github.com:IronCoreLabs/ironoxide-java.git'
                }
                licenses {
                    license {
                        name = 'AGPL-3.0'
                        url = 'https://www.gnu.org/licenses/agpl-3.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer { id = 'bobwall23'; name = 'Bob Wall'; url = 'https://github.com/bobwall23' }
                    developer { id = 'coltfred'; name = 'Colt Frederickson'; url = 'https://github.com/coltfred' }
                    developer { id = 'giarc3'; name = 'Craig Colegrove'; url = 'https://github.com/giarc3' }
                    developer { id = 'skeet70'; name = 'Murph Murphy'; url = 'https://github.com/skeet70' }
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.release
}

// Extract classes.jar from the rustls-platform-verifier AAR and place it in libs/ so it gets
// bundled into our AAR. This avoids requiring consumers to configure the unpublished rustls
// Maven repository.
task extractRustlsClasses {
    doLast {
        def aar = configurations.rustlsEmbed.singleFile
        def outputDir = file("libs")
        outputDir.mkdirs()
        copy {
            from zipTree(aar)
            include 'classes.jar'
            rename 'classes.jar', 'rustls-platform-verifier.jar'
            into outputDir
        }
    }
}

preBuild.dependsOn extractRustlsClasses

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    rustlsEmbed 'rustls:rustls-platform-verifier:latest.release'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications {
            release {
                from components.release
            }
        }
    }
}