apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    // Locked NDK version for consistent builds. We use the same version in the Example App
    // and they should remain in sync for best compatibility.
    ndkVersion "21.3.6528147"

    defaultConfig {
        // sdk version 24 required to support Java's Optional type
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'x86', 'arm64-v8a', 'x86_64'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'Assert'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            pom.groupId = 'com.ironcorelabs'
            pom.artifactId = 'ironoxide-android'
            pom.version = VERSION_NAME

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: NEXUS_USERNAME,
                               password: NEXUS_PASSWORD)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: NEXUS_USERNAME,
                               password: NEXUS_PASSWORD)
            }

            pom.project {
                name 'ironoxide-android'
                packaging 'aar'
                description 'Android library for IronOxide transform encryption'
                url 'https://github.com/IronCoreLabs/ironoxide-java'

                scm {
                    url 'https://github.com/IronCoreLabs/ironoxide-java'
                    connection 'scm:git@github.com:IronCoreLabs/ironoxide-java.git'
                    developerConnection 'scm:git@github.com:IronCoreLabs/ironoxide-java.git'
                }

                licenses {
                    license {
                        name 'AGPL-3.0'
                        url 'https://www.gnu.org/licenses/agpl-3.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'bobwall23'
                        name 'Bob Wall'
                        url 'https://github.com/bobwall23'
                    }
                    developer {
                        id 'clintfred'
                        name 'Clint Frederickson'
                        url 'https://github.com/clintfred'
                    }
                    developer {
                        id 'coltfred'
                        name 'Colt Frederickson'
                        url 'https://github.com/coltfred'
                    }
                    developer {
                        id 'ernieturner'
                        name 'Ernie Turner'
                        url 'https://github.com/ernieturner'
                    }
                    developer {
                        id 'giarc3'
                        name 'Craig Colegrove'
                        url 'https://github.com/giarc3'
                    }
                    developer {
                        id 'skeet70'
                        name 'Murph Murphy'
                        url 'https://github.com/skeet70'
                    }
                }
            }
        }
    }
}

// Only sign artifacts if we can upload them.
tasks.withType(Sign) {
    onlyIf { NEXUS_USERNAME != "" }
}
signing {
    sign configurations.archives
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
